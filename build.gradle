buildscript {
    ext.kotlin_version = '1.3.31'
    ext.junit_version = '5.4.2'
    ext.guava_version = '27.1-jre'
    ext.springboot_version = '2.1.4.RELEASE'
    ext.protoc_version = '3.7.1'
    ext.grpc_version = '1.20.0'
    ext.kethereum_version = '0.75.0'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springboot_version")
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.8'
    }
}

plugins {
    id 'idea'
    id 'java'
    id "org.jetbrains.kotlin.plugin.spring" version '1.3.31' apply false
    id 'org.jetbrains.kotlin.jvm' version '1.3.31' apply false
    id 'com.google.protobuf' version '0.8.8' apply false
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

allprojects {
    group 'com.github.ackintosh.plasmachain'
    version '1.0-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = '1.8'
            freeCompilerArgs = ['-Xjsr305=strict']
        }
    }
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = '1.8'
            freeCompilerArgs = ['-Xjsr305=strict']
        }
    }

    test {
        useJUnitPlatform()
    }

}

project(':utxo') {
    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        implementation "com.google.guava:guava:$guava_version"
        implementation "com.github.komputing.KEthereum:keccak_shortcut:$kethereum_version"
        implementation "com.github.komputing.KEthereum:rlp:$kethereum_version"
        implementation 'org.bouncycastle:bcprov-jdk15on:1.61'
        testImplementation "org.junit.jupiter:junit-jupiter:$junit_version"
    }
}

project(':node') {
    dependencies {
        implementation project(':utxo')
        implementation 'org.web3j:core:4.2.0'
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        testImplementation "org.junit.jupiter:junit-jupiter:$junit_version"
    }
}

project(':web') {
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.jetbrains.kotlin.plugin.spring'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.google.protobuf'

    dependencies {
        implementation project(':utxo')
        implementation project(':node')
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.jetbrains.kotlin:kotlin-reflect'
        implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
        implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.8"

        // gRPC
        implementation "io.grpc:grpc-netty-shaded:$grpc_version"
        implementation "io.grpc:grpc-protobuf:$grpc_version"
        implementation "io.grpc:grpc-stub:$grpc_version"
        implementation "io.github.lognet:grpc-spring-boot-starter:3.2.1"

        // testing
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    protobuf {
        protoc {
            artifact = "com.google.protobuf:protoc:$protoc_version"
        }
        plugins {
            grpc {
                artifact = "io.grpc:protoc-gen-grpc-java:$grpc_version"
            }
        }
        generateProtoTasks {
            all()*.plugins { grpc {} }
        }
    }
}
